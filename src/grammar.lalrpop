use std::str::FromStr;

use syntax::{Expr, Opcode};
use reference::Reference;

grammar;

pub Formula = {
	"=" <Expr>,
}

Expr: Box<Expr> = {
    Expr ExprOp Factor => Box::new(Expr::Op(<>)),
    Factor,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Factor: Box<Expr> = {
    Factor FactorOp Term => Box::new(Expr::Op(<>)),
    Term,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Term: Box<Expr> = {
    Int => Box::new(Expr::Integer(<>)),
    Float => Box::new(Expr::Float(<>)),
    Ref => Box::new(Expr::Reference(<>)),
    "(" <Expr> ")"
};

Int: i64 = r"[0-9]+" => i64::from_str(<>).unwrap();
Float: f64 = r"[0-9]+\.[0-9]+" => f64::from_str(<>).unwrap();
Ref: Reference = r"[a-zA-Z]{1,2}[1-9][0-9]{0,2}" => Reference::from_str(<>).unwrap();
